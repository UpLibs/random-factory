apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse-wtp'

sourceCompatibility = 1.7
group = "randomfactory"
version = "1.0"

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

jar {
    manifest {
        attributes(
                'Implementation-Title': 'Random Factory',
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date().format('yyyy-MM-dd HH:mm:ss')
        )
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "${nexusUrl}") {
                authentication(userName: "${nexusUsername}", password: "${nexusPassword}")
            }
        }
    }
}
